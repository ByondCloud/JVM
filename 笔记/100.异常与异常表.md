## 抛出异常指令

在 Java 程序中显式抛出异常的操作（throw 语句）都是由 athrow 指令来实现的。

除了使用throw语句显示抛出异常情况之外，<font color="orange">JVM规范还规定了许多运行时异常会在其他Java虚拟机指令检测到异常状况时自动抛出</font>。例如，在之前介绍的整数运算时，当除数为零时，虚拟机会在idiv或1div指令中抛出ArithmeticException异常。

正常情况下，操作数栈的压入弹出都是一条条指令完成的。唯一的例外情况是<font color="orange">在抛异常时，Java虚拟机会清除操作数栈上的所有内容，而后将异常实例压入调用者操作数栈上。</font>

异常及异常的处理：

过程一：异常对象的生成过程 ---> throw(手动/自动) ---> 指令：athrow

过程二：异常的处理：抓抛模型 try-catch-finally ---> 使用异常表



### 例子一

```java
public void throwZero(int i){
    if(i == 0){
        throw new RuntimeException("参数值为0");
    }
}
```

```java
 0 iload_1
 1 ifne 14 (+13)  // 如果i != 0 就跳转到14行return
 4 new #2 <java/lang/RuntimeException> // 新建了一个运行时异常
 7 dup // 复制了一个
 8 ldc #3 <参数值为0>   // String入栈
10 invokespecial #4 <java/lang/RuntimeException.<init> : (Ljava/lang/String;)V> // 执行运行时异常的构造器
13 athrow // 抛出异常
14 return
```

### 例子二

```java
public void throwOne(int i) throws RuntimeException,IOException{
    if(i == 1){
        throw new RuntimeException("参数值为1");
    }
}
```

```java
 0 iload_1
 1 iconst_1
 2 if_icmpne 15 (+13) // 参数i和常量1是否不相等，不相等跳转到15return
 5 new #2 <java/lang/RuntimeException>
 8 dup
 9 ldc #5 <参数值为1>
11 invokespecial #4 <java/lang/RuntimeException.<init> : (Ljava/lang/String;)V>
14 athrow  // 抛出异常
15 return
```

多了一个异常

![image-20230508105902422](image/100.%E5%BC%82%E5%B8%B8%E4%B8%8E%E5%BC%82%E5%B8%B8%E8%A1%A8/image-20230508105902422.png)



### 例子三

运行时异常，并没有athrow

```java
public void throwArithmetic() {
    int i = 10;
    int j = i / 0;
    System.out.println(j);
}
```

```java
 0 bipush 10
 2 istore_1
 3 iload_1
 4 iconst_0
 5 idiv
 6 istore_2
 7 getstatic #6 <java/lang/System.out : Ljava/io/PrintStream;>
10 iload_2
11 invokevirtual #7 <java/io/PrintStream.println : (I)V>
14 return
```





## 处理异常

在 Java 虚拟机中，<font color="orange">处理异常（catch 语句）</font>不是由字节码指令来实现的（早期使用 jsr、ret 指令），而是<font color="orange">采用异常表来完成的。</font>



## 异常表

如果一个方法定义了一个try-catch 或者try-finally的异常处理，就会创建一个异常表。它包含了每个异常处理或者finally块的信息。异常表保存了每个异常处理信息。比如：

- 起始位置

- 结束位置

- 程序计数器记录的代码处理的偏移地址

- 被捕获的异常类在常量池中的索引

<font color="orange">当一个异常被抛出时，JVM会在当前的方法里寻找一个匹配的处理，如果没有找到，这个方法会强制结束并弹出当前栈帧</font>，并且异常会重新抛给上层调用的方法（在调用方法栈帧）。如果在所有栈帧弹出前仍然没有找到合适的异常处理，这个线程将终止。如果这个异常在最后一个非守护线程里抛出，将会导致JVM自己终止，比如这个线程是个main线程。

<font color="orange">不管什么时候抛出异常，如果异常处理最终匹配了所有异常类型，代码就会继续执行</font>。在这种情况下，如果方法结束后没有抛出异常，仍然执行finally块，在return前，它直接跳到finally块来完成目标



## 例子一

```java
public void tryCatch(){
    try{
        File file = new File("d:/hello.txt");
        FileInputStream fis = new FileInputStream(file);
        String info = "hello!";
    }catch (FileNotFoundException e) {
        e.printStackTrace();
    }
    catch(RuntimeException e){
        e.printStackTrace();
    }
}
```

```java
 0 new #8 <java/io/File>
 3 dup
 4 ldc #9 <d:/hello.txt>
 6 invokespecial #10 <java/io/File.<init> : (Ljava/lang/String;)V>
 9 astore_1
10 new #11 <java/io/FileInputStream>
13 dup
14 aload_1
15 invokespecial #12 <java/io/FileInputStream.<init> : (Ljava/io/File;)V>
18 astore_2
19 ldc #13 <hello!>
21 astore_3
22 goto 38 (+16)
25 astore_1
26 aload_1
27 invokevirtual #15 <java/io/FileNotFoundException.printStackTrace : ()V>
30 goto 38 (+8)
33 astore_1
34 aload_1
35 invokevirtual #16 <java/lang/RuntimeException.printStackTrace : ()V>
38 return
```

![image-20230508110531353](image/100.%E5%BC%82%E5%B8%B8%E4%B8%8E%E5%BC%82%E5%B8%B8%E8%A1%A8/image-20230508110531353.png)



如果代码中没有出现错误，那么从偏移量0 - 22，然后跳转到38结束，和catch代码块没有关系了

但是！如果中间出现了异常，那么就要看异常表，如图，0 - 22出现了相对应的异常就看跳转PC跳到哪里去，接着执行就完了



## 例子二

```java
//思考：如下方法返回结果为多少？
public static String func() {
    String str = "hello";
    try{
        return str;
    }
    finally{
        str = "cloud";
    }
}
```

```java
 0 ldc #17 <hello>  // 入栈
 2 astore_0 // 局部变量表0 = hello
 3 aload_0  // 入栈
 4 astore_1 // 局部变量表0 = hello， 1 = hello
 5 ldc #18 <cloud> // 入栈
 7 astore_0 // 局部变量表0 = cloud， 1 = hello
 8 aload_1 // hello入栈
 9 areturn // hello return出去
10 astore_2
11 ldc #18 <cloud>
13 astore_0
14 aload_2
15 athrow
```

