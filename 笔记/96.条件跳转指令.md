条件跳转指令通常和比较指令结合使用。在条件跳转指令执行前，一般可以先用比较指令进行栈顶元素的准备，然后进行条件跳转。

条件跳转指令有：ifeq、iflt、ifle、ifne、ifgt、ifge、ifnull、ifnonnull。这些指令都接收两个字节的操作数，用于计算跳转的位置（16 位符号整数作为当前位置的 offset）。

它们的统一含义为：<font color="orange">弹出栈顶元素，测试它是否满足某一条件，如果满足条件，则跳转到给定位置。</font>

|   指令    |   符号   |               说明               |
| :-------: | :------: | :------------------------------: |
|   ifeq    |   ==0    |   当栈顶int类型数值等于0时跳转   |
|   ifne    |   !=0    |  当栈顶int类型数值不等于0时跳转  |
|   iflt    |    <0    |   当栈顶int类型数值小于0时跳转   |
|   ifle    |   <=0    | 当栈顶int类型数值小于等于0时跳转 |
|   ifgt    |    >0    |   当栈顶int类型数组大于0时跳转   |
|   ifge    |   >=0    | 当栈顶int类型数值大于等于0时跳转 |
|  ifnull   |   null   |           为null时跳转           |
| ifnonnull | not null |          不为null时跳转          |

**注意：**

1. 与前面运算规则一致

- 对于 boolean、byte、char、short 类型的条件分支比较操作，都是使用 int 类型的比较指令完成。
- 对于 long、float、double 类型的条件分支比较操作，则会先执行相应类型的比较运算指令，运算指令会返回一个整型值到操作数栈中，随后再执行 int 类型的条件分支比较操作来完成整个分支跳转。

1. 由于各类型的比较最终都会转为 int 类型的比较操作，所以 Java 虚拟机提供的 int 类型的条件分支指令是最为丰富和强大的。





## 例子一

```java
int a = 0;
if(a == 0){
    a = 10;
}else{
    a = 20;
}
```

```java
 0 iconst_0
 1 istore_1
 2 iload_1
 3 ifne 12 (+9) // 满足这个条件就跳转12，不满足就不跳
 6 bipush 10
 8 istore_1
 9 goto 15 (+6)  // 后面会酱，无条件跳转
12 bipush 20
14 istore_1
15 return
```

## 例子2

```java
float f1 = 9;
float f2 = 10;
System.out.println(f1 < f2);// true
```

```java
 0 ldc #2 <9.0>
 2 fstore_1
 3 ldc #3 <10.0>
 5 fstore_2
 6 getstatic #4 <java/lang/System.out : Ljava/io/PrintStream;>
 9 fload_1
10 fload_2
11 fcmpg // 比较运算符 栈顶小是1，栈顶大是-1，等于就是0
12 ifge 19 (+7) // -1去比较是否大于等于0
15 iconst_1 // 1入栈
16 goto 20 (+4)
19 iconst_0 
20 invokevirtual #5 <java/io/PrintStream.println : (Z)V>
23 return
```

## 例子三

```java
int i1 = 10;
long l1 = 20;
System.out.println(i1 > l1);
```

```java
 0 bipush 10
 2 istore_1
 3 ldc2_w #6 <20>
 6 lstore_2
 7 getstatic #4 <java/lang/System.out : Ljava/io/PrintStream;>
10 iload_1
11 i2l       // int -> long
12 lload_2
13 lcmp      // -1
14 ifle 21 (+7) // -1 <= 0
17 iconst_1
18 goto 22 (+4)
21 iconst_0 // 入栈0
22 invokevirtual #5 <java/io/PrintStream.println : (Z)V> // 0是false
25 return
```

## 例子四

```java
public int compare4(double d) {
    if (d > 50.0) {
        return 1;
    } else {
        return -1;
    }
}
```

```java
 0 dload_1 // d入栈
 1 ldc2_w #8 <50.0> // 50入栈
 4 dcmpl   // d与50比较，假如现在d = 10.0，输出为 -1
 5 ifle 10 (+5) // -1 <= 0
 8 iconst_1
 9 ireturn
10 iconst_m1 // -1 入栈
11 ireturn
```

