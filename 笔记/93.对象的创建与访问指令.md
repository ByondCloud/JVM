Java 是面向对象的程序设计语言，虚拟机平台从字节码层面就对面向对象做了深层次的支持。有一系列指令专门用于对象操作，可进一步细分为创建指令、字段访问指令、数组操作指令和类型检查指令。

## 创建指令

虽然类实例和数组都是对象，但 Java 虚拟机对类实例和数组的创建与操作使用了不同的字节码指令

### 创建类实例的指令：

创建类实例的指令：new

- 它接收一个操作数，为指向常量池的索引，表示要创建的类型，执行完成后，将对象的引用压入栈。

```java
public void newInstance() {
    Object obj = new Object();
    File file = new File("cloud.avi");
}
```

```java
 0 new #2 <java/lang/Object>
 3 dup
 4 invokespecial #1 <java/lang/Object.<init> : ()V>
 7 astore_1
 8 new #3 <java/io/File>
11 dup
12 ldc #4 <cloud.avi>
14 invokespecial #5 <java/io/File.<init> : (Ljava/lang/String;)V>
17 astore_2
18 return
```

![image-20230508032350125](image/93.%E5%AF%B9%E8%B1%A1%E7%9A%84%E5%88%9B%E5%BB%BA%E4%B8%8E%E8%AE%BF%E9%97%AE%E6%8C%87%E4%BB%A4/image-20230508032350125.png)

### 创建数组的指令：

创建数组的指令：newarray、anewarray、multianewarray

- newarray：创建基本类型数组
- anewarray：创建引用类型数组
- multianewarray：创建多维数组

上述创建指令可以用于创建对象或者数组，由于对象和数组在 Java 中的广泛使用，这些指令的使用频率也很高。

```java
public void newArray() {
    int[] intArray = new int[10];              // newarray 10 (int)
    Object[] objArray = new Object[10];        // anewarray #2 <java/lang/Object>
    int[][] mintArray = new int[10][10];       //  multianewarray #6 <[[I> dim 2
    String[][] strArray = new String[10][];    // anewarray #7 <[Ljava/lang/String;>
    String[][] strArray2 = new String[10][5];  // multianewarray #8 <[[Ljava/lang/String;> dim 2
}
```

我们都知道数组长度需要先定义，才能使用，那么我们这个strArray只定义了一维数组，一维数组中装的数组并没有定义，因此jvm把这个二维数组看成一维数组，如果你要用，就要初始化一维数组中的数组，没初始化就不用了，这就是为啥是anewarray了



## 字段访问指令

对象创建后，就可以通过对象访问指令获取对象实例或数组实例中的字段或者数组元素。

- <font color="orange">访问类字段（static 字段，或者称为类变量）的指令：getstatic、putstatic</font>
- <font color="orange">访问类实例字段（非 static 字段，或者称为实例变量）的指令：getfield、putfield</font>

举例：以 getstatic 指令为例，它含有一个操作数，为指向常量池的 Fieldref 索引，它的作用就是获取 Fieldref 指定的对象或者值，并将其压入操作数栈。

```java
public void sayHello() {
  System.out.println("Hello"); 
}
```

```java
0 getstatic #9 <java/lang/System.out : Ljava/io/PrintStream;>   // System.out入栈
3 ldc #10 <hello>  // hello入栈
5 invokevirtual #11 <java/io/PrintStream.println : (Ljava/lang/String;)V> // 方法，新的栈帧，把System.out和hello都出栈
8 return
```



```java
public void setOrderId(){
    Order order = new Order();
    order.id = 1001;
    System.out.println(order.id);
    Order.name = "ORDER";
    System.out.println(Order.name);
}
class Order{
    int id;
    static String name;
}
```

```java
 0 new #12 <com/cloud/java/Order>   // order入栈
 3 dup   // 复制一份
 4 invokespecial #13 <com/cloud/java/Order.<init> : ()V>   // 构造器
 7 astore_1   // order对象放到局部变量表中1的位置
 8 aload_1    // 入栈
 9 sipush 1001     // 1001入栈
12 putfield #14 <com/cloud/java/Order.id : I>   // 通过order地址找到order，给order.id赋值
15 getstatic #9 <java/lang/System.out : Ljava/io/PrintStream;>    // System.out入栈
18 aload_1   // order对象入栈
19 getfield #14 <com/cloud/java/Order.id : I>  // order对象出栈，order.id入栈
22 invokevirtual #15 <java/io/PrintStream.println : (I)V>  // 执行println方法
25 ldc #16 <ORDER>   // ORDER字符串入栈
27 putstatic #17 <com/cloud/java/Order.name : Ljava/lang/String;>   // ORDER字符串出栈，赋值给静态变量name
30 getstatic #9 <java/lang/System.out : Ljava/io/PrintStream;>     // System.out入栈
33 getstatic #17 <com/cloud/java/Order.name : Ljava/lang/String;>  // Order.name入栈
36 invokevirtual #11 <java/io/PrintStream.println : (Ljava/lang/String;)V>  // 执行println方法
39 return
```



## 数组操作指令

数组操作指令主要有：xastore和xaload指令。具体为：

- 把一个数组元素加载到操作数栈的指令：baload、caload、saload、iaload、laload、faload、daload、aaload

- 将一个操作数栈的值存储到数组元素中的指令：bastore、castore、sastore、iastore、lastore、fastore、dastore、aastore

|   数组类型    | 加载指令 | 存储指令 |
| :-----------: | :------: | :------: |
| byte(boolean) |  baload  | bastore  |
|     char      |  caload  | castore  |
|     short     |  saload  | sastore  |
|      int      |  iaload  | iastore  |
|     long      |  laload  | lastore  |
|     float     |  faload  | fastore  |
|    double     |  daload  | dastore  |
|   reference   |  aaload  | aastore  |

取数组长度的指令：arraylength。该指令弹出栈顶的数组元素，获取数组的长度，将长度压入栈。

**说明**

指令xaload表示将数组的元素压栈，比如saload、caload分别表示压入short数组和char数组。指令xaload在执行时，要求操作数中栈顶元素为数组索引i，栈顶顺位第2个元素为数组引用a，该指令会弹出栈顶这两个元素，并将a[i]重新压入栈。

xastore则专门针对数组操作，以iastore为例，它用于给一个int数组的给定索引赋值。<font color="orange">在iastore执行前，操作数栈顶需要以此准备3个元素：值、索引、数组引用，iastore会弹出这3个值，并将值赋给数组中指定索引的位置。</font>

```java
int[] intArray = new int[10];
intArray[3] = 20;
System.out.println(intArray[1]);
boolean[] arr = new boolean[10];
arr[1] = true;
```

```java
 0 bipush 10              // 10入操作数栈
 2 newarray 10 (int)      // 新建一个int数组，长度为10
 4 astore_1               // intArray地址放到局部变量表中
 5 aload_1                // intArray入栈
 6 iconst_3               // 3入栈
 7 bipush 20              // 20入栈
 9 iastore                // 将20存储进intArray3的位置，看上面橙字有解释
10 getstatic #9 <java/lang/System.out : Ljava/io/PrintStream;>
13 aload_1
14 iconst_1
15 iaload
16 invokevirtual #15 <java/io/PrintStream.println : (I)V>
19 bipush 10
21 newarray 4 (boolean)
23 astore_2
24 aload_2
25 iconst_1
26 iconst_1 // 1是True
27 bastore
28 return
```

## 类型检查指令

检查类实例或数组类型的指令：instanceof、checkcast。

- 指令checkcast用于检查类型强制转换是否可以进行。如果可以进行，那么checkcast指令不会改变操作数栈，否则它会抛出ClassCastException异常
- 指令instanceof用来判断给定对象是否是某一个类的实例，它会将判断结果压入操作数栈

```java
public String checkCast(Object obj) {
    if (obj instanceof String) {
        return (String) obj;
    } else {
        return null;
    }
}
```

```java
 0 aload_1
 1 instanceof #18 <java/lang/String>  // 判断是否为String的实例
 4 ifeq 12 (+8)
 7 aload_1      // 判断之后obj就出栈了，所以需要再次入栈
 8 checkcast #18 <java/lang/String>   // 强转String
11 areturn
12 aconst_null
13 areturn
```

