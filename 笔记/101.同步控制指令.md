Java虚拟机支持两种同步结构：<font color="orange">方法级的同步</font>和<font color="orange">方法内部一段指令序列的同步</font>，这两种同步都是使用monitor来支持的

## 方法级的同步

方法级的同步：<font color="orange">是隐式的</font>，即无须通过字节码指令来控制，它实现在方法调用和返回操作之中。虚拟机可以从方法常量池的方法表结构中的ACC_SYNCHRONIZED访问标志得知一个方法是否声明为同步方法；

当调用方法时，调用指令将会检查方法的ACC_SYNCHRONIZED访问标志是否设置。

- 如果设置了，执行线程将先持有同步锁，然后执行方法。<font color="orange">最后</font>在方法完成（无论是正常完成还是非正常完成）时<font color="orange">释放同步锁</font>。

- 在方法执行期间，执行线程持有了同步锁，其他任何线程都无法再获得同一个锁。

- 如果一个同步方法执行期间抛出了异常，并且在方法内部无法处理此异常，那这个同步方法所持有的锁将在异常抛到同步方法之外时自动释放。



### 测试用例

```java
private int i = 0;
public synchronized void add(){  // 这里的synchronized加和不加的字节码区别
	i++;
}
```

```java
 0 aload_0
 1 dup
 2 getfield #2 <com/cloud/java1/SynchronizedTest.i : I>
 5 iconst_1
 6 iadd
 7 putfield #2 <com/cloud/java1/SynchronizedTest.i : I>
10 return
```

字节码是一样的，唯一的区别只有访问标识的区别

<img src="image/101.%E5%90%8C%E6%AD%A5%E6%8E%A7%E5%88%B6%E6%8C%87%E4%BB%A4/image-20230508115815857.png" alt="image-20230508115815857" style="zoom: 67%;" />![image-20230508115829924](image/101.%E5%90%8C%E6%AD%A5%E6%8E%A7%E5%88%B6%E6%8C%87%E4%BB%A4/image-20230508115829924.png)<img src="image/101.%E5%90%8C%E6%AD%A5%E6%8E%A7%E5%88%B6%E6%8C%87%E4%BB%A4/image-20230508115815857.png" alt="image-20230508115815857" style="zoom: 67%;" />![image-20230508115829924](image/101.%E5%90%8C%E6%AD%A5%E6%8E%A7%E5%88%B6%E6%8C%87%E4%BB%A4/image-20230508115829924.png)

这段代码和普通的无同步操作的代码没有什么不同，没有使用 monitorenter 和 monitorexit 进行同步区控制。这是因为，对于同步方法而言，当虚拟机通过方法的访问标识符判断是一个同步方法时，会自动在方法调用前进行加锁，当同步方法执行完毕后，不管方法是正常结束还是有异常抛出，均会由虚拟机释放这个锁。因此，对于同步方法而言，monitorenter 和 monitorexit 指令是隐式存在的，并未直接出现在字节码中。



## 方法内指令指令序列的同步【待定，JUC回头看】

同步一段指令集序列：通常是由 Java 中的 synchronized 语句块来表示的。JVM 的指令集有 monitorenter 和 monitorexit 两条指令来支持 synchronized 关键字的语义。

当一个线程进入同步代码块时，它使用 monitorenter 指令请求进入。如果当前对象的监视器计数器为 0，则它会被准许进入，若为 1，则判断持有当前监视器的线程是否为自己，如果是，则进入，否则进行等待，直到对象的监视器计数器为 0，才会被允许进入同步块。

当线程退出同步块时，需要使用 monitorexit 声明退出。在 Java 虚拟机中，任何对象都有一个监视器与之相关联，用来判断对象是否被锁定，当监视器被持有后，对象处于锁定状态。

指令 monitorenter 和 monitorexit 在执行时，都需要在操作数栈顶压入对象，之后 monitorenter 和 monitorexit 的锁定和释放都是针对这个对象的监视器进行的。

编译器必须确保无论方法通过何种方式完成，方法中调用过的每条 monitorenter 指令都必须执行其对应的 monitorexit 指令，而无论这个方法是正常结束还是异常结束。

为了保证在方法异常完成时 monitorenter 和 monitorexit 指令依然可以正确配对执行，<font color="orange">编译器会自动产生一个异常处理器，这个异常处理器声明可处理所有的异常</font>，它的目的就是用来执行 monitorexit 指令。



### 案例

```java
public class SynchronizedTest {

    private int i = 0;
    private Object obj = new Object();
    
    public void subtract(){

        synchronized (obj){
            i--;
        }
        
    }
}
```

```java
 0 aload_0 // this入栈
 1 getfield #4 <com/cloud/java1/SynchronizedTest.obj : Ljava/lang/Object;> // 获取obj，覆盖this的位置
 4 dup // 复制obj
 5 astore_1 // 将复制的obj放到局部变量表中的1位置
 6 monitorenter  // 同步监视器，会把obj的对象头中的所状态从0改成1【加锁】
 7 aload_0 // 取出this
 8 dup //复制this
 9 getfield #2 <com/cloud/java1/SynchronizedTest.i : I>  // 获取i，把复制的this消耗掉了
12 iconst_1 // 常量1入栈
13 isub // i - 1 = -1
14 putfield #2 <com/cloud/java1/SynchronizedTest.i : I> // 将-1 赋值给i，同时也消耗掉了this
17 aload_1 // obj入栈
18 monitorexit // 同步监视器，会把obj的对象头中的所状态从1改成0【解锁】
19 goto 27 (+8)  // 结束
22 astore_2   // 这里会往局部变量表中放一个Exception其子类...
23 aload_1  // obj入栈，主要是要把obj解锁
24 monitorexit // 同步监视器，会把obj的对象头中的所状态从1改成0【解锁】
25 aload_2  // Exception入栈
26 athrow   // 抛出异常
27 return
```

![image-20230508121607002](image/101.%E5%90%8C%E6%AD%A5%E6%8E%A7%E5%88%B6%E6%8C%87%E4%BB%A4/image-20230508121607002.png)

any是任何异常，只要在同步代码块中出现异常，就跳到偏移量22去执行，总之就是要让对象头的锁从1改成0